buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21

        ndk {
            if (Boolean.valueOf(JNI_DEBUG)) {
                moduleName 'gif'
                abiFilter 'armeabi-v7a'
                ldLibs 'jnigraphics', 'android', 'log'
                cFlags '-DDEBUG'
            } else {
                moduleName 'gif'
                ldLibs 'jnigraphics', 'android'
                abiFilter 'all'
                cFlags '-fvisibility=hidden -Ofast'
            }
        }
    }
    buildTypes.debug.jniDebuggable Boolean.valueOf(JNI_DEBUG)

    def flavors = ['armeabi', 'armeabi-v7a', 'x86', 'mips', 'arm64-v8a', 'mips64', 'x86_64']
    boolean applyFlavors = false
    gradle.startParameter.taskNames.each() {
        String normalizedTaskName = it.toLowerCase(Locale.US)
        flavors.each() {
            applyFlavors |= normalizedTaskName.contains(it)
        }
    }
    if (applyFlavors) {
        productFlavors {
            flavors.each() {
                String flavor = it
                "${it}" {
                    ndk {
                        abiFilter flavor
                    }
                }
            }
            fat
        }
    }
}


allprojects {
    version = VERSION_NAME
    group = GROUP
    repositories {
        mavenCentral()
    }
    dependencies {
        provided 'com.android.support:support-annotations:21.0.3'
    }
}

apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'

afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
        }
    }
}
